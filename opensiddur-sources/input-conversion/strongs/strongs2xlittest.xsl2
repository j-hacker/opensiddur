<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="2.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
  xmlns:tei="http://www.tei-c.org/ns/1.0"
  xmlns:j="http://jewishliturgy.org/ns/jlptei/1.0"
  xmlns:osis="http://www.bibletechnologies.net/2003/OSIS/namespace"
  xmlns:xd="http://www.pnp-software.com/XSLTdoc" 
  xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#" 
  xmlns:cc="http://web.resource.org/cc/"
  exclude-result-prefixes="osis xd rdf cc">
  <xsl:import href="../../common/params.xsl2" />
  <xsl:import href="../fill-template.xsl2" />  
  <xsl:output indent="yes" encoding="utf-8" method="xml" />
  <xd:doc type="stylesheet">
    <xd:short>Create Strong's dictionary word transliteration test file.
    </xd:short>
    <xd:copyright>Copyright 2010 Ze'ev Clementson
    </xd:copyright>
    <xd:detail>
      <p>The output is a JLPTEI XML file that can be used to test</p>
      <p>transliterations. Set transliteration on in transliteration-fsd</p>
      <p>and run transform.sh</p>
      <p>This program is free software: you can redistribute it
        and/or modify
        it under the terms of the GNU Lesser General Public License as
        published by
        the Free Software Foundation, either version 3 of the License, or
        (at your option) any later version.</p>

      <p>This program is distributed in the hope that it will be
        useful,
        but WITHOUT ANY WARRANTY; without even the implied warranty of
        MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
        GNU Lesser General Public License for more details.</p>

      <p>
        You should have received a copy of the GNU Lesser General Public
        License
        along with this program. If not, see
        <a href="http://www.gnu.org/licenses/">http://www.gnu.org/licenses/
        </a>
        .
      </p>
    </xd:detail>
  </xd:doc>
  
  <xd:doc>
    <xd:short>Top-level entry point for all processing</xd:short>
  </xd:doc>
  <xsl:template match="/">
    <xsl:call-template name="main"/>
  </xsl:template>
  
  <xd:doc>
    <xd:short>Populate template and launch main processing logic for program</xd:short>
  </xd:doc>
  <xsl:template name="main">
    <xsl:variable name="bibl-content" as="element(tei:bibl)">
			<tei:bibl xml:id="biblStrongs">
	      <tei:ptr target="bibliography.xml#strongs-hebrew" />
			</tei:bibl>
    </xsl:variable>
    <xsl:variable name="license-content" as="element()+">
      <p xml:lang="en" xmlns="http://www.tei-c.org/ns/1.0">
        To the extent possible under law,
        the contributors who associated
        <ref type="license" target="http://www.creativecommons.org/publicdomain/zero/1.0">Creative Commons Zero
        </ref>
        with this work have waived all copyright and related or
        neighboring rights to this work.
      </p>
      <rdf:RDF>
        <cc:License rdf:about="http://creativecommons.org/publicdomain/zero/1.0/">
          <cc:legalcode
            rdf:resource="http://creativecommons.org/publicdomain/zero/1.0/legalcode" />
          <cc:permits rdf:resource="http://creativecommons.org/ns#Reproduction" />
          <cc:permits rdf:resource="http://creativecommons.org/ns#Distribution" />
        </cc:License>
      </rdf:RDF>
    </xsl:variable>
    
    <xsl:apply-templates select="$template" mode="fill-template">
      <xsl:with-param tunnel="yes" name="title" as="element(tei:title)+">
        <tei:title xml:lang="en">Transliteration test file</tei:title>
      </xsl:with-param>
      <xsl:with-param tunnel="yes" name="fsdDecl" as="element(tei:fsdDecl)">
        <tei:fsdDecl>
          <tei:fsdLink type="transliteration" target="transliteration-fsd.xml#fsTransliteration"/>
        </tei:fsdDecl>
      </xsl:with-param>
      <xsl:with-param tunnel="yes" name="conditionGrp" as="element(j:conditionGrp)">
        <j:conditionGrp>
          <tei:fs type="transliteration" xml:id="transliterator_on">
            <tei:f name="table"><tei:symbol value="strongs"/></tei:f>
          </tei:fs>
        </j:conditionGrp>
      </xsl:with-param>
      <xsl:with-param tunnel="yes" name="links" as="element(j:links)">          
        <j:links>
          <tei:link type="set" targets="#main #transliterator_on"/>
					<tei:link type="source" targets="#main #biblStrongs"/>
        </j:links>
      </xsl:with-param>    
      <xsl:with-param tunnel="yes" name="body" as="element(tei:body)">
        <tei:body xml:lang="en" xml:id="main">
          <xsl:apply-templates select="osis:osis/osis:osisText/osis:div/osis:div"/>
        </tei:body>
      </xsl:with-param>
      <xsl:with-param name="license" tunnel="yes" as="element()+" select="$license-content" />
      <xsl:with-param name="bibl" tunnel="yes" as="element(tei:bibl)" select="$bibl-content" />
    </xsl:apply-templates>
  </xsl:template>
  
  <xd:doc>
    <xd:short>Create new dictionary entry</xd:short>
  </xd:doc>
  <xsl:template match="osis:div">
    <tei:entry>
      <xsl:attribute name="xml:id"><xsl:value-of select="osis:w/@ID"/></xsl:attribute>
      <xsl:apply-templates select="osis:w[@ID]"/>
    </tei:entry>
  </xsl:template>
  
  <xd:doc>
    <xd:short>Output Strongs#, Hebrew word, pronunciation, transliteration</xd:short>
  </xd:doc>
  <xsl:template match="osis:w[@ID]">
    <tei:form>
      <tei:orth type="number"><xsl:value-of select="../osis:w/@ID"/></tei:orth>
      <tei:orth type="hebrew" xml:lang="he"><xsl:value-of select="@lemma"/></tei:orth>
      <tei:pron><xsl:value-of select="@POS"/></tei:pron>
      <tei:orth type="xlit-strongs"><xsl:value-of select="@xlit"/></tei:orth>
        <tei:orth type="xlit">
        <j:segGen type="transliteration" xml:lang="he"><xsl:value-of select="@lemma"/></j:segGen>
      </tei:orth>
    </tei:form>
  </xsl:template>

</xsl:stylesheet>
