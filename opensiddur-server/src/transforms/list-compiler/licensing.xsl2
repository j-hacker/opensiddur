<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet
  version="2.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:tei="http://www.tei-c.org/ns/1.0"
  xmlns:j="http://jewishliturgy.org/ns/jlptei/1.0"
  xmlns:jx="http://jewishliturgy.org/ns/jlp-processor"
  xmlns:func="http://jewishliturgy.org/ns/functions/xslt"
  xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#"
  xmlns:cc="http://web.resource.org/cc/"
  xmlns:xs="http://www.w3.org/2001/XMLSchema"
  xmlns:xd="http://www.pnp-software.com/XSLTdoc"
  exclude-result-prefixes="#all"
  >
  <xsl:import href="../../common/common.xsl2"/>
  <xsl:import href="../follow-uri.xsl2"/>
  <xsl:import href="../intl-strings.xsl2"/>
  <xd:doc type="stylesheet">
    <xd:short>Given a completed document, and all its sources 
    (//jx:relationship), find all unique licensing statements
    and apply templates to them.</xd:short>
    <xd:copyright>Copyright 2010-2011 Efraim Feinstein (LGPL 3+)</xd:copyright>
    <xd:detail>
        <p>This program is free software: you can redistribute it and/or modify
        it under the terms of the GNU Lesser General Public License as published by
        the Free Software Foundation, either version 3 of the License, or
        (at your option) any later version.</p>

        <p>This program is distributed in the hope that it will be useful,
        but WITHOUT ANY WARRANTY; without even the implied warranty of
        MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
        GNU Lesser General Public License for more details.</p>

        <p>You should have received a copy of the GNU Lesser General Public License
        along with this program.  If not, see 
        <a href="http://www.gnu.org/licenses/">http://www.gnu.org/licenses/</a>.
        </p>
      </xd:detail>
      <xd:svnId>$Id: licensing.xsl2 706 2011-02-21 01:18:10Z efraim.feinstein $</xd:svnId>
  </xd:doc>

  <xsl:param name="string-table" as="xs:string" select="'strings.xml'"/>
  
  <xsl:variable name="string-table-doc" as="document-node()" select="doc('strings.xml')"/>

  <xd:doc>Language code for primary document language</xd:doc>
  <xsl:param name="primary-language" select="'en'" as="xs:string"/>

  <xd:doc>
    <xd:short>Generate a license list.  The context can be anywhere in the document</xd:short>
  </xd:doc>
  <xsl:template name="generate-license-list" as="element(tei:div)">
  <!-- xsl:message>
+++++root element is: <xsl:sequence select="root(.)"/>
+++++distinct-values = <xsl:sequence select="root(.)/distinct-values((//@jx:document-uri, //@jx:source-document-uri))"/>
  </xsl:message-->
    <xsl:variable name="included-files" as="xs:string*" 
      select="root(.)/distinct-values((//@jx:document-uri, //@jx:source-document-uri))"/>
    
    <xsl:variable name="all-license-statements"
      select="for $uri in $included-files
        return func:follow-uri($uri, .,-1)//tei:availability"
      as="element(tei:availability)*"/>
      
    <xsl:variable name="distinct-legalcode" as="xs:string*"
      select="distinct-values($all-license-statements//cc:legalcode/@rdf:resource)"
    />
    <xsl:sequence select="func:debug($distinct-legalcode, 'distinct-legalcode', $debug-info)"/>
    
    <xsl:variable name="unique-licenses" as="element(tei:availability)*"
      select="for $legalcode in $distinct-legalcode return
        $all-license-statements[descendant::cc:legalcode/@rdf:resource=$legalcode][1]"/>
    <xsl:sequence select="func:debug($unique-licenses, 'unique-licenses', $debug-info)"/>
    
    <xsl:variable name="additional-statements"
      as="element(tei:p)*"
      select="$all-license-statements//tei:p[@n='additional']"
      />
    <xsl:variable name="additional-attributions"
      as="element(tei:p)*"
      select="$all-license-statements//tei:p[@n='attribution']"
      />
    
    <tei:div type="license-list">
      <xsl:attribute name="xml:lang" select="$primary-language"/>
      <tei:head>
        <xsl:sequence select="func:get-string($string-table-doc,'licensing-header', $primary-language)"/>
      </tei:head>
      <tei:p>
        <xsl:sequence select="func:get-string($string-table-doc,'licensing-statement', $primary-language)"/>
      </tei:p>
      <xsl:apply-templates mode="licensing" select="$unique-licenses"/>
      
      <xsl:if test="$additional-statements">
        <tei:div type="additional-license-statements">
          <tei:p>
            <xsl:sequence
              select="func:get-string($string-table-doc,'licensing-additional', $primary-language)"/>
          </tei:p>
          <xsl:sequence select="$additional-statements"/>
        </tei:div>
      </xsl:if>
      <xsl:if test="$additional-attributions">
        <tei:div type="additional-license-attributions">
          <tei:p>
            <xsl:sequence select="func:get-string($string-table-doc,'licensing-attribution', $primary-language)"/>
          </tei:p>
          <xsl:sequence select="$additional-attributions"/>
        </tei:div>
      </xsl:if>
    </tei:div>
  </xsl:template>  
  
  <xd:doc>
    <xd:short>Convert availability to tei:ab</xd:short>
  </xd:doc>
  <xsl:template mode="licensing" match="tei:availability">
    <tei:ab type="license">
      <xsl:apply-templates/>
    </tei:ab>
  </xsl:template>
  
</xsl:stylesheet>
