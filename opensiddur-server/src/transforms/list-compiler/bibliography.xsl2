<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet
  version="2.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:tei="http://www.tei-c.org/ns/1.0"
  xmlns:j="http://jewishliturgy.org/ns/jlptei/1.0"
  xmlns:jx="http://jewishliturgy.org/ns/jlp-processor"
  xmlns:func="http://jewishliturgy.org/ns/functions/xslt"
  xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#"
  xmlns:cc="http://web.resource.org/cc/"
  xmlns:xs="http://www.w3.org/2001/XMLSchema"
  xmlns:xd="http://www.pnp-software.com/XSLTdoc"
  exclude-result-prefixes="#all"
  >
  <xsl:import href="../../common/common.xsl2"/>
  <xsl:import href="../intl-strings.xsl2"/>
  <xsl:import href="lc-common.xsl2"/>
  <xd:doc type="stylesheet">
    <xd:short>Handle bibliography</xd:short>
    <xd:copyright>Copyright 2010-2011 Efraim Feinstein (LGPL 3+)</xd:copyright>
    <xd:detail>
        <p>This program is free software: you can redistribute it and/or modify
        it under the terms of the GNU Lesser General Public License as published by
        the Free Software Foundation, either version 3 of the License, or
        (at your option) any later version.</p>

        <p>This program is distributed in the hope that it will be useful,
        but WITHOUT ANY WARRANTY; without even the implied warranty of
        MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
        GNU Lesser General Public License for more details.</p>

        <p>You should have received a copy of the GNU Lesser General Public License
        along with this program.  If not, see 
        <a href="http://www.gnu.org/licenses/">http://www.gnu.org/licenses/</a>.
        </p>
      </xd:detail>
      <xd:svnId>$Id: bibliography.xsl2 687 2011-01-23 23:36:48Z efraim.feinstein $</xd:svnId>
  </xd:doc>

  <!-- xsl:variable name="string-table" as="document-node()"
    select="doc(strings.xml)"/-->

  <xd:doc>
    <xd:short>Generate a bibliography</xd:short>
  </xd:doc>
  <xsl:template name="generate-bibliography-list">
    <xsl:variable name="sources" select="key('relationships-by-type', 'source')" as="element(jx:relationship)*"/>
    <xsl:variable name="references"
      select="key('relationships-by-type', 'bibl')"  as="element(jx:relationship)*"/>
      
    <xsl:if test="exists($sources)">  
      <tei:div type="bibliography">
        <xsl:attribute name="xml:lang" select="$primary-language"/>
        <tei:head>
          <xsl:sequence select="func:get-string($string-table-doc, 'bibliography-header-sources',$primary-language)"/>
        </tei:head>
        <xsl:call-template name="make-bibliography-list">
          <xsl:with-param name="list-pointers" as="element()*" select="$sources"/>
        </xsl:call-template>
      </tei:div>
    </xsl:if>
    <xsl:if test="exists($references)">
      <tei:div type="bibliography">
        <xsl:attribute name="xml:lang" select="$primary-language"/>
        <tei:head>
          <xsl:sequence 
            select="func:get-string($string-table-doc, 'bibliography-header-references',$primary-language)"/>
        </tei:head>
        <xsl:call-template name="make-bibliography-list">
          <xsl:with-param name="list-pointers" as="element()*" select="$references"/>
        </xsl:call-template>
      </tei:div>
    </xsl:if>
  </xsl:template>
  
  <xd:doc>
    <xd:short>Helper function to make a bibliography list, given
    one of the jx:* elements that has pointers to bibliography URIs</xd:short>
  </xd:doc>
  <xsl:template name="make-bibliography-list" as="element(tei:listBibl)">
    <xsl:param name="list-pointers" as="element()*"/>

		<tei:listBibl>
      <xsl:for-each-group select="$list-pointers/jx:linked-relationship//tei:biblStruct" group-by="(@jx:id,@xml:id)[1]">
        <xsl:sort select="string-join((
          for $name-element in (
          tei:analytic/tei:author/tei:name,
          tei:analytic/tei:editor/tei:name,
          tei:monogr/tei:author/tei:name,
          tei:monogr/tei:editor/tei:name, 
          tei:series/tei:author/tei:name,
          tei:series/tei:editor/tei:name
          ) 
          return func:name-sort-key($name-element),
          tei:analytic/tei:title,
          tei:monogr/tei:title,
          tei:series/tei:title
          ), ' ')"/>
        <xsl:apply-templates select="current-group()[1]" mode="bibliography"/>
      </xsl:for-each-group>
    </tei:listBibl>
  </xsl:template>
  
  <xd:doc>
    <xd:short>A bibliography containing a pointer should not be copied</xd:short>
  </xd:doc>
  <xsl:template match="tei:biblStruct" mode="bibliography">
    <xsl:sequence select="."/>
  </xsl:template>  
  
  <xd:doc>
    <xd:short>tei:bibl inside a bibliography probably contains a biblStruct that was a pointer.  
    If it doesn't, I have no idea what to do with it.</xd:short>
  </xd:doc>
  <xsl:template match="tei:bibl" mode="bibliography">
    <xsl:apply-templates mode="#current"/>
  </xsl:template>
  
</xsl:stylesheet>
