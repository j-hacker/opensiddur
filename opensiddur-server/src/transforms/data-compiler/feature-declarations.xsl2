<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet
	version="2.0" 
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	xmlns:tei="http://www.tei-c.org/ns/1.0"
	xmlns:j="http://jewishliturgy.org/ns/jlptei/1.0"
  xmlns:jx="http://jewishliturgy.org/ns/jlp-processor"
	xmlns:func="http://jewishliturgy.org/ns/functions/xslt"
	xmlns:xs="http://www.w3.org/2001/XMLSchema"
	xmlns:xd="http://www.pnp-software.com/XSLTdoc"
  xmlns:debug="http://jewishliturgy.org/ns/debug/1.0"
	extension-element-prefixes="xs xd func debug"
	>
  
  <xsl:import href="../../common/common.xsl2"/>	
  <xsl:import href="../follow-uri.xsl2"/>
  
	<xd:doc type="stylesheet">
		<xd:short>
      Functions to use for feature declarations during a transform
		</xd:short>
		<xd:copyright>Copyright 2010 Efraim Feinstein</xd:copyright>

		<xd:detail>
			<p>This program is free software: you can redistribute it and/or modify
			it under the terms of the GNU Lesser General Public License as published by
			the Free Software Foundation, either version 3 of the License, or
			(at your option) any later version.</p>

			<p>This program is distributed in the hope that it will be useful,
			but WITHOUT ANY WARRANTY; without even the implied warranty of
			MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
			GNU Lesser General Public License for more details.</p>

			<p>You should have received a copy of the GNU Lesser General Public License
				along with this program.  If not, see 
				<a href="http://www.gnu.org/licenses/">http://www.gnu.org/licenses/</a>.
			</p>
		</xd:detail>
		<xd:svnId>$Id: feature-declarations.xsl2 687 2011-01-23 23:36:48Z efraim.feinstein $</xd:svnId>
	</xd:doc>
  
  <xd:doc>Search for all declarations</xd:doc>
  <xsl:key name="declared-feature-structures" match="tei:fsdLink|tei:fsDecl" use="@type"/>

  <xd:doc>Search for all named features</xd:doc>
  <xsl:key name="declared-feature-names" match="tei:fDecl" use="@name"/>

  <xd:doc>
    <xd:short>Return the declaration of the given feature structure</xd:short>
    <xd:param name="type">Feature structure type</xd:param>
    <xd:param name="context">Context</xd:param>
  </xd:doc>
  <xsl:function name="func:get-fs-declaration" as="element(tei:fsDecl)">
    <xsl:param name="type" as="xs:string"/>
    <xsl:param name="context" as="node()"/>
    <xsl:variable name="declaration" as="element()" 
      select="key('declared-feature-structures', $type, root($context))"/>
    <xsl:sequence select="
      if ($declaration instance of element(tei:fsdLink))
      then func:follow-uri($declaration/@target, $context, -1)
      else $declaration"/>
  </xsl:function>

  <xd:doc>
    <xd:short>Return the declaration of a given feature</xd:short>
    <xd:detail>This will run into trouble if the fsDecl uses @baseTypes</xd:detail>
    <xd:param name="type">Feature structure type</xd:param>
    <xd:param name="name">Feature name</xd:param>
    <xd:param name="context">Context</xd:param>
  </xd:doc>
  <xsl:function name="func:get-feature-declaration" as="element(tei:fDecl)">
    <xsl:param name="type" as="xs:string"/>
    <xsl:param name="name" as="xs:string"/>
    <xsl:param name="context" as="node()"/>
  
    <xsl:sequence select="key('declared-feature-names', $name, 
      func:get-fs-declaration($type, $context))"/>
  </xsl:function>

</xsl:stylesheet>
